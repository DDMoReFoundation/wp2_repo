magni2004_data = dataObj {
    SOURCE {
        srcfile : {file = "magni2004_data_ORIGINAL.csv", inputFormat is nonmemFormat}
    }

    DECLARED_VARIABLES {
        Y :: observation
    }

    DATA_INPUT_VARIABLES {
        ID : {use is id}
        TIME : {use is idv}
        DV : {use is dv, variable = Y}
        GLUC : {use is covariate}
        EVID : {use is evid}
    }
}

magni2004_pri = priorObj {
    NON_CANONICAL_DISTRIBUTION {
    PRIOR_SOURCE {
        data1 : {column = ["data_k01", "data_k12", "data_k21"], file = "prior_magni2004.csv", inputFormat is csv}
    }

    INPUT_PRIOR_DATA {
         :: {column = ["data_k01", "data_k12", "data_k21"], matrixVar = data_k_joint, src = data1}
    }

    }

    PRIOR_VARIABLE_DEFINITION {
        data_k_joint :: matrix
        POP_joint ~ MultiEmpirical(data=data_k_joint)
        POP_K01 = POP_joint
        POP_K12 = POP_joint
        POP_K21 = POP_joint
        POP_m ~ Normal(mean=MU_POP_m, sd=SIGMA_POP_m)
        POP_alpha ~ Normal(mean=MU_POP_alpha, sd=SIGMA_POP_alpha)
        POP_beta ~ Normal(mean=MU_POP_beta, sd=SIGMA_POP_beta)
        POP_x0 ~ Normal(mean=MU_POP_x0, sd=SIGMA_POP_x0)
        POP_h ~ Normal(mean=MU_POP_h, sd=SIGMA_POP_h)
        CV = 0.06
    }

    PRIOR_PARAMETERS {
        GB = 89
        MU_POP_m = 0.5
        MU_POP_alpha = 0.06
        MU_POP_beta = 11
        MU_POP_x0 = 1.8
        MU_POP_h = GB
        SIGMA_POP_m = 0.5
        SIGMA_POP_alpha = 0.06
        SIGMA_POP_beta = 11
        SIGMA_POP_x0 = 1.8
        SIGMA_POP_h = GB*0.03
    }
}

magni2004_par = parObj {
    VARIABILITY {
        CV : {fix = true, value = 0.06}
    }

    STRUCTURAL {
        POP_K01 : {fix = true, value = 0.0618}
        POP_K12 : {fix = true, value = 0.0492}
        POP_K21 : {fix = true, value = 0.0479}
        POP_m : {lo = 0, value = 0.6}
        POP_alpha : {lo = 0, value = 0.06}
        POP_beta : {lo = 0, value = 11}
        POP_x0 : {lo = 0, value = 1.8}
        POP_h : {fix = true, value = 89}
    }
}

magni2004_mdl = mdlObj {
    OBSERVATION {
        Y : {additive = 1.0E-4, eps = eps_RES_CP1, prediction = CP1, proportional = CV, type is combinedError1}
    }

    VARIABILITY_PARAMETERS {
        CV
    }

    VARIABILITY_LEVELS {
        DV : {level = 1, type is observation}
        ID : {level = 2, type is parameter}
    }

    STRUCTURAL_PARAMETERS {
        POP_K01
        POP_K12
        POP_K21
        POP_m
        POP_alpha
        POP_beta
        POP_x0
        POP_h
    }

    IDV {
        T
    }

    COVARIATES {
        GLUC
    }

    RANDOM_VARIABLE_DEFINITION(level=DV) {
        eps_RES_CP1 ~ Normal(mean=0, var=1)
    }

    MODEL_PREDICTION {
    DEQ {
        ISR = if (GLUC>h) then m*X else 0
        dYi = if (GLUC>h) then -alpha*(Yi-beta*(GLUC-h)*(0.05551)) else -alpha*Yi
        CP1 : {deriv = -(K01+K21)*CP1+K12*CP2+ISR, init = 0, x0 = 0}
        CP2 : {deriv = -K12*CP2+K21*CP1, init = 0, x0 = 0}
        X : {deriv = -ISR+Yi, init = (x0new), x0 = 0}
        Yi : {deriv = dYi, init = 0, x0 = 0}
    }

    }

    INDIVIDUAL_VARIABLES {
        K01 = POP_K01
        K12 = POP_K12
        K21 = POP_K21
        m = POP_m
        alpha = POP_alpha
        beta = POP_beta
        x0 = POP_x0
        h = POP_h
        x0new = x0*1000
    }
}

magni2004_NONMEM_task = taskObj {
    ESTIMATE {
        set algo is foce

    }
}

magni2004_BUGS_task = taskObj {
    ESTIMATE {
        set algo is mcmc
    TARGET_SETTINGS(target="BUGS") {
        set burnin = 1000
        set nchains = 1
        set niter = 5000
        set odesolver = "LSODA"
        set winbugsgui = "false"
    }

    }
}

mymog_NONMEM = mogObj {
    OBJECTS {
        magni2004_data : {type is dataObj}
        magni2004_pri : {type is priorObj}
        magni2004_mdl : {type is mdlObj}
        magni2004_NONMEM_task : {type is taskObj}
    }
}
