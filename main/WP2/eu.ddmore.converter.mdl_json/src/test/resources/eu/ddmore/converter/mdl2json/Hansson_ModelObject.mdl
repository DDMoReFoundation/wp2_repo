Hansson2013_mdl = mdlobj{

    VARIABILITY_LEVELS{
        ID : { level=2, type = model }
        DV : { level=1 , type=observation}
   }
    COVARIATES{
        CL
   }

   STRUCTURAL_PARAMETERS{
      POP_IMAX
      POP_IC50 # Common IC50 for the four biomarkers which were found to be highly correlated

      #VEGF
      POP_BM0  # baseline VEGF
      POP_MRT 
      POP_HILL
      POP_TVSLP
                
      #sVEGFR-2
      POP_BM02
      POP_MRT2 
      POP_HILL2
      
      #sVEGFR-3
      POP_BM03
      POP_MRT3 

      #sKIT
      POP_BM0S
      POP_MRTS
      
      #Residual Error
      POP_RES_VEGF_ADD 
      POP_RES_sVEGFR2_ADD 
      POP_RES_sVEGFR2_PROP
      POP_RES_sVEGFR3_ADD
      POP_RES_sKIT_ADD        
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_BM0
      OMEGA_BM02
      OMEGA_BM03
      OMEGA_BM0S
      
      OMEGA_MRT_VEGFs
      OMEGA_MRT_sKIT
      
      OMEGA_IC50
      OMEGA_IC502
      OMEGA_IC503
      OMEGA_IC50S
              
      OMEGA_TVSLP
      OMEGA_TVSLPS     
      SIGMA_RES_W
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_BM0 ~ Normal(mean=0, var=OMEGA_BM0)
      eta_BM02 ~ Normal(mean=0, var=OMEGA_BM02)
      eta_BM03 ~ Normal(mean=0, var=OMEGA_BM03)
      eta_BM0S ~ Normal(mean=0, var=OMEGA_BM0S)
      
      eta_MRT_VEGFs ~ Normal(mean=0, var=OMEGA_MRT_VEGFs)
      eta_MRT_sKIT ~ Normal(mean=0, var=OMEGA_MRT_sKIT)
   
      eta_IC50 ~ Normal(mean=0, var=OMEGA_IC50)
      eta_IC502 ~ Normal(mean=0, var=OMEGA_IC502)
      eta_IC503 ~ Normal(mean=0, var=OMEGA_IC503)
      eta_IC50S ~ Normal(mean=0, var=OMEGA_IC50S)
      
      eta_TVSLP ~ Normal(mean=0, var=OMEGA_TVSLP)
      eta_TVSLPS ~ Normal(mean=0, var=OMEGA_TVSLPS)   
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      BM0 : {type=linear, trans=log, pop = POP_BM0, ranEff=eta_BM0} 
      BM02 : {type=linear, trans=log, pop = POP_BM02, ranEff=eta_BM02} 
      BM03 : {type=linear, trans=log, pop = POP_BM03, ranEff=eta_BM03}
      BM0S : {type=linear, trans=log, pop = POP_BM0S, ranEff=eta_BM0S} 
               
      IMAX1 = POP_IMAX
      IMAX2 = POP_IMAX
      IMAX3 = POP_IMAX
      IMAXS = POP_IMAX
     
      IC50 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC50} 
      IC502 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC502} 
      IC503 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC503}
      IC50S : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC50S} 
 
      HILL = POP_HILL
      HILL2 = POP_HILL2
     
      MRT1 : {type=linear, trans=log, pop = POP_MRT, ranEff=eta_MRT_VEGFs} 
      MRT2 : {type=linear, trans=log, pop = POP_MRT2, ranEff=eta_MRT_VEGFs} 
      MRT3 : {type=linear, trans=log, pop = POP_MRT3, ranEff=eta_MRT_VEGFs}
      MRTS : {type=linear, trans=log, pop = POP_MRTS, ranEff=eta_MRT_sKIT} 
     
      TVSLP = POP_TVSLP/1000
      DPSLP : {type=linear, trans=log, pop = TVSLP, ranEff=eta_TVSLP} 
         
      TVSLPS = POP_TVSLP/1000
      DPSLPS : {type=linear, trans=log, pop = TVSLPS, ranEff=eta_TVSLPS} 
         
      # Derived parameters
      KOUT = 1/MRT1
      KOUT2 = 1/MRT2
      KOUT3 = 1/MRT3
      KOUTS = 1/MRTS

      KIN2 = BM02*KOUT2
      KIN3 = BM03*KOUT3
     
      }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      AUC = DOS/CL
      DP1 = BM0*(1+DPSLP*T)        # Linear disease progression model for VEGF
      DPS = BM0S*(1+DPSLPS*T)      # Linear disease progression model for SKIT

      KIN = DP1*KOUT
      KINS = DPS*KOUTS
       
    DEQ{
       EFF = IMAX1*AUC^HILL /(IC50^HILL+AUC^HILL)     # VEGF  : sigmoid Imax drug effect relationship
       EFF2 = IMAX2*AUC^HILL2/(IC502^HILL2+AUC^HILL2) # VEGFR2: sigmoid Imax drug effect relationship
       EFF3 = IMAX3*AUC/(IC503+AUC)                      # V EGFR3: Imax drug effect relationship
       EFFS = IMAXS*AUC/(IC50S+AUC)                      # SKIT  : Imax drug effect relationship
       
       VEGF : {deriv=KIN -KOUT*(1-EFF)*VEGF, init=BM0, wrt=T}
       sVEGFR2 : {deriv=KIN2*(1-EFF2)-KOUT2*sVEGFR2, init=BM02, wrt=T}
       sVEGFR3 : {deriv=KIN3*(1-EFF3)-KOUT3*sVEGFR3, init=BM03, wrt=T}
       sKIT : {deriv= KINS*(1-EFFS)-KOUTS*sKIT, init=BM0S, wrt=T}
    }
   }# end MODEL_PREDICTION

 
   RANDOM_VARIABLE_DEFINITION(level=DV){
      eps_RES_W ~ Normal(mean=0, var=SIGMA_RES_W)
   }

   OBSERVATION{
      VEGF_obs : {type=continuous, trans=log, error=additiveError(additive=POP_RES_VEGF_ADD),
                           prediction=VEGF, eps=eps_RES_W}
      
      sVEGFR2_obs : {type=continuous, trans=log,
                   error=combinedError2(additive=POP_RES_sVEGFR2_ADD, proportional=POP_RES_sVEGFR2_PROP,f=sVEGFR2),
                prediction=sVEGFR2, eps=eps_RES_W}

      sVEGFR3_obs : {type=continuous, trans=log,
                       error=additiveError(additive=POP_RES_sVEGFR3_ADD), prediction=sVEGFR3, eps=eps_RES_W}
      
       sKIT_obs : {type=continuous, trans=log,
                   error=additiveError(additive=POP_RES_sKIT_ADD), prediction=sKIT, eps=eps_RES_W}
   }# end OBSERVATION

} # end of model object
