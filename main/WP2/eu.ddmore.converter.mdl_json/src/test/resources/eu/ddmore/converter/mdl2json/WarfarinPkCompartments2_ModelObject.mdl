warfarin_PK_Compartments2_mdl = mdlobj {
    IDV{ T } 

    COVARIATES{
        WT
        logtWT = log(WT/70)
    }

    VARIABILITY_LEVELS{
        ID : { level=2, type=parameter  }
        DV : { level=1, type=observation }
    }

    STRUCTURAL_PARAMETERS {
        POP_F1
        POP_KT
        POP_K1
        POP_CL
        POP_V
        POP_KA
        POP_TLAG
        BETA_F1_WT
        BETA_CL_WT
        BETA_V_WT
    } # end STRUCTURAL_PARAMETERS
    
    VARIABILITY_PARAMETERS {
        PPV_F1
        PPV_KT
        PPV_K1
        PPV_CL
        PPV_V
        PPV_KA
        PPV_TLAG
        RUV_PROP
        RUV_ADD
    } # end VARIABILITY_PARAMETERS 
    
    RANDOM_VARIABLE_DEFINITION(level=ID) {
        ETA_KT ~ Normal(mean = 0, sd = PPV_KT)
        ETA_K1 ~ Normal(mean = 0, sd = PPV_K1)
        ETA_F1 ~ Normal(mean = 0, sd = PPV_F1)
        ETA_CL ~ Normal(mean = 0, sd = PPV_CL)
        ETA_V ~ Normal(mean = 0, sd = PPV_V)
        ETA_KA ~ Normal(mean = 0, sd = PPV_KA)
        ETA_TLAG ~ Normal(mean = 0, sd = PPV_TLAG) # We define correlation here
    } # end RANDOM_VARIABLE_DEFINITION 

    
    INDIVIDUAL_VARIABLES { # This maps to the "Type 3" individual parameter definition in PharmML
        F1 : { type = linear, trans = log, pop = POP_F1, fixEff = {coeff=BETA_F1_WT , cov = logtWT }, ranEff = ETA_F1 }
        K1 : { type = linear, trans = log, pop = POP_K1, ranEff = ETA_K1 }
        KT : { type = linear, trans = log, pop = POP_KT, ranEff = ETA_KT }
        CL : { type = linear, trans = log, pop = POP_CL, fixEff = {coeff=BETA_CL_WT, cov =logtWT}, ranEff = ETA_CL }
        V : { type = linear, trans = log, pop = POP_V, fixEff =  {coeff=BETA_V_WT , cov = logtWT }, ranEff = ETA_V }
        KA : { type = linear, trans = log, pop = POP_KA, ranEff = ETA_KA }
        TLAG : { type = linear, trans = log, pop = POP_TLAG, ranEff = ETA_TLAG } 
    } # end INDIVIDUAL_VARIABLES

    MODEL_PREDICTION {
        PKMACRO{
        # defines two compartments a latent and central and two depots for dose.
        # Dosing to latent compartment is oral bolus
        # Dosing to central compartment is IV
        DEP1 : { macro = iv, to=CENTRAL }
        CENTRAL : { macro=compartment, volume=V}
        { macro = elimination, from=CENTRAL, cl=CL }
        GUT : { macro=oral, ka=KA, p=F1, to=LATENT }
        LATENT : { macro=compartment, volume=1 }
        { macro=transfer, from=LATENT, to=CENTRAL, kt=KT }
        { macro=elimination, from=LATENT, k=K1 }
        }
        SCALE = V
        CC = CENTRAL / V
    } # end MODEL_PREDICTION
    
    RANDOM_VARIABLE_DEFINITION(level=DV){
        EPS_Y ~ Normal(mean = 0, var = 1) # This maps the standard error model in PharmML. The error model is predefined.
    }

    OBSERVATION {
        Y : { type = continuous, error = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, f = CC),
                  eps = EPS_Y, prediction = CC } 
    } # end OBSERVATION
} # end of model object