warfarin_PK_ODE_mdl = mdlobj {

    VARIABILITY_LEVELS{
        ID : { level=2, type=model }
        DV : { level=1, type=observation }
   }

    COVARIATES{
        WT
        logtWT = log(WT/70)
    }

    STRUCTURAL_PARAMETERS {
        POP_CL
        POP_V
        POP_KA
        POP_TLAG
        BETA_CL_WT
        BETA_V_WT
    } # end STRUCTURAL_PARAMETERS
    
    VARIABILITY_PARAMETERS {
        PPV_CL
        PPV_V
        PPV_KA
        PPV_TLAG
        RUV_PROP
        RUV_ADD
    } # end VARIABILITY_PARAMETERS 
    
    RANDOM_VARIABLE_DEFINITION(level=ID) {
        ETA_CL ~ Normal(mean = 0, sd = PPV_CL)
        ETA_V ~ Normal(mean = 0, sd = PPV_V)
        ETA_KA ~ Normal(mean = 0, sd = PPV_KA)
        ETA_TLAG ~ Normal(mean = 0, sd = PPV_TLAG)
    } # end RANDOM_VARIABLE_DEFINITION 
    
    INDIVIDUAL_VARIABLES { # This maps to the "Type 3" individual parameter definition in PharmML
        CL : { type = linear, trans = log, pop = POP_CL, fixEff = [ BETA_CL_WT ], cov = [ logtWT ], ranEff = ETA_CL }
        V : { type = linear, trans = log, pop = POP_V, fixEff = [ BETA_V_WT ], cov = [ logtWT ], ranEff = ETA_V }
        KA : { type = linear, trans = log, pop = POP_KA, ranEff = ETA_KA }
        TLAG : { type = linear, trans = log, pop = POP_TLAG, ranEff = ETA_TLAG } 
    } # end INDIVIDUAL_VARIABLES
    
    MODEL_PREDICTION {
        D # dosing variable
        TD # dosing time
        k = CL/V
        CC = 0 when T - TD < TLAG
             otherwise (D/V) * (KA/(KA-k) * (exp(-k * (T -TD-TLAG) - exp(-KA*(T-TD-TLAG)))))
    } # end MODEL_PREDICTION

    RANDOM_VARIABLE_DEFINITION(level=DV){
        EPS_Y ~ Normal(mean = 0, var = 1) # This maps the standard error model in PharmML. The error model is predefined. 
    }

    OBSERVATION {
        Y : { type = continuous, error = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, f = CC),
                  eps = EPS_Y, prediction = CC } 
    } # end OBSERVATION
    
    #MODEL_OUTPUT_VARIABLES {
    #   ID logtWT CL V KA TLAG Y
    #} # end MODEL_OUTPUT_VARIABLES
} # end of model object
