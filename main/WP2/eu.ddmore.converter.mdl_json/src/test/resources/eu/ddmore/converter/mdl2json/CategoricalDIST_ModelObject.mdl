Categorical_DIST_mdl = mdlobj{
   IDV{ CP }

   VARIABILITY_LEVELS{
    ID : { level=2, type=parameter }
    DV : { level=1, type=observation }
   }

   STRUCTURAL_PARAMETERS{
      Beta
      Lgt0
      Lgt1
      Lgt2
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      PPV_EVENT
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_PPV_EVENT ~ Normal(mean=0, var=PPV_EVENT )
   }# end RANDOM_VARIABLE_DEFINITION

   GROUP_VARIABLES{
      EDRUG = Beta * CP
      B0 = Lgt0
      B1 = B0 + Lgt1
      B2 = B1 + Lgt2
   }
   
   INDIVIDUAL_VARIABLES{
      A0 = B0 + EDRUG + eta_PPV_EVENT
      A1 = B1 + EDRUG + eta_PPV_EVENT
      A2 = B2 + EDRUG + eta_PPV_EVENT
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
      P0 = 1/(1+exp(-A0))  #invLogit(A0)
      P1 = 1/(1+exp(-A1))  #invLogit(A1)
      P2 = 1/(1+exp(-A2))  #invLogit(A2)
      
      Prob0 = P0
      Prob1 = P1 - P0
      Prob2 = P2 - P1
      Prob3 = 1 - P2
   }# end MODEL_PREDICTION

   OBSERVATION{
       Y : {type=categorical, categories=[0,1,2,3], probabilities=[Prob0, Prob1, Prob2, Prob3]} 
    }# end ESTIMATION

} # end of model object