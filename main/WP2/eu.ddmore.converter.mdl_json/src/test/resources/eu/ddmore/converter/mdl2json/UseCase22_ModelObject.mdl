warfarin_PK_BOV_mdl = mdlobj{
   COVARIATES{
    WT
   }

   VARIABILITY_LEVELS{
    ID : { level=3, type=parameter }
    OCC : { level=2, type=parameter }
    DV : { level=1, type=observation }
   }

   STRUCTURAL_PARAMETERS{
      #THETA
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      #OMEGA
      BSV_CL
      BSV_V
      BOV_CL
      BOV_V
      BOV_KA
      BOV_TLAG
      BSV_KA
      BSV_TLAG
      #SIGMA
      RUV_PROP
      RUV_ADD
   }# end VARIABILITY_PARAMETERS

   GROUP_VARIABLES{
      # Covariate model
      GRPCL=POP_CL*(WT/70)^ 0.75  ## Type 1 - Equation type
      GRPV=POP_V*WT/70
      GRPKA=POP_KA
      GRPLG=POP_TLAG
   }# end GROUP_VARIABLES

   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_BSV_CL~ Normal(mean=0, var=BSV_CL)
      eta_BSV_V~ Normal(mean=0, var=BSV_V)
      eta_BSV_KA~ Normal(mean=0, var=BSV_KA)
      eta_BSV_TLAG~ Normal(mean=0, var=BSV_TLAG)
#     eta_RUV ~ Normal(mean=0, var=PPV_RUV)
   }# end RANDOM_VARIABLE_DEFINITION

   RANDOM_VARIABLE_DEFINITION(level=OCC){
      eta_BOV_CL~ Normal(mean=0, var=BOV_CL)
      eta_BOV_V~ Normal(mean=0, var=BOV_V)
      eta_BOV_KA~ Normal(mean=0, var=BOV_KA)
      eta_BOV_TLAG~ Normal(mean=0, var=BOV_TLAG)
   }# end RANDOM_VARIABLE_DEFINITION

   RANDOM_VARIABLE_DEFINITION(level=DV){
      eps_RUV_PROP~ Normal(mean=0, var=RUV_PROP)
      eps_RUV_ADD~ Normal(mean=0, var=RUV_ADD)
#     eps ~ Normal(mean=0, var=1)    
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      # Individual parameters
      # eta_PPV_CL = eta_BOV_CL + eta_BOV_CL  ## Test case
      CL=GRPCL*exp(eta_BSV_CL+eta_BOV_CL) ## Type 1 - Equation type
      # ALTERNATIVELY CL : {type=general, trans=log, pop=GRPCL, ranEff=[eta_BSV_CL,eta_BOV_CL] } ## Type 2 - Guassian with general covariate model
      # SHOULD NOT WORK CL : {type=general, trans=log, pop=GRPCL, ranEff=eta_BSV_CL+eta_BOV_CL } ## Test case for expression 
      # SHOULD NOT WORK CL : {type=general, trans=log, pop=GRPCL, ranEff=eta_PPV_CL } ## Test case for expression 
      V=GRPV*exp(eta_BSV_V+eta_BOV_V)
      KA=GRPKA*exp(eta_BSV_KA+eta_BOV_KA)
      ALAG1=GRPLG*exp(eta_BSV_TLAG+eta_BOV_TLAG)
      # NM-TRAN scale for central compartment
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
    PKMACRO{
        DEP { macro=oral, to=Ac, ka=KA, tlag=ALAG1 }
        Ac: {macro=compartment, volume=V}
        { macro=elimination, from=Ac, cl=CL}}
     CONC = Ac/V

   }# end MODEL_PREDICTION

   OBSERVATION{
      Y = CONC*(1+eps_RUV_PROP)+eps_RUV_ADD  # Type 1 - equation type
      ## Y = CONC*(1+eps_RUV_PROP)+eps_RUV_ADD*exp(eta_RUV)  # eta on eps general
      ## sdCp = eps_RUV_ADD*exp(eta_RUV) # eta on eps standard
      ## Y = CONC + sdCp*eps  # eta on eps
      ## Y : { type = continuous, error = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, f = CC),
      ##            eps = EPS_Y, prediction = CC } 
   }# end OBSERVATION

} # end of model object