warfarin_PK_BOV_mdl = mdlobj{
   IDV { TIME }

   COVARIATES{
    WT
    logtWT = log(WT/70)
   }

   VARIABILITY_LEVELS{
    ID : { level=3, type=parameter }
    OCC : { level=2, type=parameter }
    DV : { level=1, type=observation }
   }

   STRUCTURAL_PARAMETERS{
      #THETA
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
      BETA_CL_WT
      BETA_V_WT
      RUV_PROP
      RUV_ADD
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      #OMEGA
      BSV_CL
      BSV_V
      BOV_CL
      BOV_V
      BOV_KA
      BOV_TLAG
      BSV_KA
      BSV_TLAG
   }# end VARIABILITY_PARAMETERS

   RANDOM_VARIABLE_DEFINITION(level=ID){
      eta_BSV_CL~ Normal(mean=0, var=BSV_CL)
      eta_BSV_V~ Normal(mean=0, var=BSV_V)
      eta_BSV_KA~ Normal(mean=0, var=BSV_KA)
      eta_BSV_TLAG~ Normal(mean=0, var=BSV_TLAG)
   }# end RANDOM_VARIABLE_DEFINITION

   RANDOM_VARIABLE_DEFINITION(level=OCC){
      eta_BOV_CL~ Normal(mean=0, var=BOV_CL)
      eta_BOV_V~ Normal(mean=0, var=BOV_V)
      eta_BOV_KA~ Normal(mean=0, var=BOV_KA)
      eta_BOV_TLAG~ Normal(mean=0, var=BOV_TLAG)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      # Individual parameters
      CL : { type = linear, trans = log, pop = POP_CL, fixEff = {coeff=BETA_CL_WT, cov=logtWT} , ranEff=[eta_BSV_CL, eta_BOV_CL]}
      V : { type = linear, trans = log, pop = POP_V, fixEff = {coeff=BETA_V_WT, cov=logtWT} , ranEff=[eta_BSV_V, eta_BOV_V] }
      KA : { type = linear, trans = log, pop = POP_KA, ranEff = [eta_BSV_KA, eta_BOV_KA] }
      TLAG : { type = linear, trans = log, pop = POP_TLAG, ranEff = [eta_BSV_TLAG, eta_BOV_TLAG] } 
      }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
        DEQ {  
            RATEIN = GUT * KA when TIME >= TLAG otherwise 0
            GUT : { deriv =(- RATEIN), init = 0, x0 = 0 }
            CENTRAL : { deriv =(RATEIN - CL * CENTRAL / V), init = 0, x0 = 0 } 
        } # end DEQ block
        CC = CENTRAL / V 
   }# end MODEL_PREDICTION

    RANDOM_VARIABLE_DEFINITION(level=DV) {
        EPS_Y ~ Normal(mean = 0, var = 1) 
    } # end RANDOM_VARIABLE_DEFINITION 
    
    OBSERVATION {
        Y : { type = continuous, error = combinedError1(additive = RUV_ADD, proportional = RUV_PROP, f = CC),
                  eps = EPS_Y, prediction = CC } 
    } # end OBSERVATION

} # end of model object