;O'REILLY RA, AGGELER PM. STUDIES ON COUMARIN ANTICOAGULANT DRUGS
;INITIATION OF WARFARIN THERAPY WITHOUT A LOADING DOSE.
;CIRCULATION 1968;38:169-177
;
;O'REILLY RA, AGGELER PM, LEONG LS. STUDIES OF THE COUMARIN ANTICOAGULANT
;DRUGS: THE PHARMACODYNAMICS OF WARFARIN IN MAN.
;JOURNAL OF CLINICAL INVESTIGATION 1963;42(10):1542-1551
;
$PROBLEM    WARFARIN PK
$INPUT      ID TIME WT AGE SEX AMT RATX DVID DV MDV
$DATA       warfpk.csv IGNORE=#
$SUBROUTINE ADVAN2 TRANS2
$OMEGA  0.1  ;      PPVCL
 0.1  ;       PPVV
 0.1  ;      PPVKA
 0.1  ;     PPVLAG
$PK


   ; COVARIATE MODEL
   TVCL=THETA(1)
   TVV=THETA(2)*WT/70
   TVKA=THETA(3)
   TVLG=THETA(4)

   ; MODEL FOR RANDOM BETWEEN SUBJECT VARIABILITY
   CL=TVCL*EXP(ETA(1))
   V=TVV*EXP(ETA(2))
   KA=TVKA*EXP(ETA(3))
   ALAG1=TVLG*EXP(ETA(4))


   ; SCALE CONCENTRATIONS
   S2=V

$ERROR

   Y=F*EXP(EPS(1))+EPS(2)
   IPRED=F

$THETA  (0.001,0.1) ; CL
 (0.001,8) ; V
 (0.001,0.362) ; KA
 (0.001,1) ; LAG
$SIGMA  0.1  ;    RUVPROP
 0.1  ;     RUVADD
$ESTIMATION MAX=9990 SIG=3 PRINT=1 METHOD=COND INTER NOABORT
$ESTIMATION METHOD=ITS INTERACTION NITER=100 PRINT=1 NOABORT SIGL=8
            CTYPE=3
$ESTIMATION METHOD=IMPMAP INTERACTION NITER=20 ISAMPLE=300 PRINT=1
            NOABORT SIGL=8
$ESTIMATION METHOD=IMP INTERACTION NITER=20 ISAMPLE=1000 PRINT=1
            NOABORT SIGL=6
$ESTIMATION NBURN=500 NITER=500 METHOD=SAEM INTERACTION PRINT=10
            SIGL=6 ISAMPLE=2
;;;$EST METHOD=DIRECT INTERACTION NBURN=2000 NITER=4000 ISAMPLE=2000 PRINT=10 SIGL=8
$COVARIANCE
$TABLE      ID TIME DVID Y ONEHEADER NOPRINT FILE=warf_cov_est.fit
$TABLE      ID SEX AGE WT CL V ETA(1) ETA(2) ONEHEADER NOAPPEND
            NOPRINT FILE=warf_cov_est.fit

