### Model object
# The model object receives a list of input data variables from the data object and parameters (constants)from the parameter object.
# The parameter and variable names in the model object must be matched by a similar name in the parameter and data objects.
# Note that variables in the data object and parameters may be defined in the parameter object that are not referenced in the model object.
# Model variables are derived from the input data variables and parameters using algebraic expressions.
tumour_size_mdl = mdlobj{

   INPUT_VARIABLES{
# A name for each input data variable is required. Special uses of input data variables are indicated by the use attribute if the data variable name is not a standard name. Standard names may include ID, IDV, AMT, DV, MDV, etc. similar to standard data items recognized by NM-TRAN or Monolix. The type option is not required but may be useful as metadata. This kind of metadata (with an option to recode) could alternatively be specified in the data object.
      ID=list(use=id,level=2)
      TIME=list(use=idv,units="h")
      AMT=list(use=amt,units="mg")
      DV=list(type=continuous,use=dv,level=1)
   }# end INPUT_VARIABLES

# The STRUCTURAL_PARAMETERS Block defines the structural model parameters.
   STRUCTURAL_PARAMETERS{
# A name for each parameter is required. Other attributes such as units are optional.
# Units may be used to achieve consistency between the parameter, data and model object values.
# This could be implemented during the WP2 translation process.

      #THETA
      POP_SIZE0
      POP_TOVER
      POP_AE50
      POP_TEQ
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
# The VARIABILITY_PARAMETERS Block defines the variability parameters.
      #OMEGA
      PPV_SIZE0
      PPV_TOVER
      PPV_AE50
      #SIGMA
      RUV_CV
      RUV_SD
   }# end VARIABILITY_PARAMETERS

   GROUP_VARIABLES{
# The GROUP_VARIABLES block is used to express covariate models i.e. the fixed effect differences in parameters predictable from data variables.
# If there are no covariate effects to model then this block is not required.
# NONMEM may be able to take advantage of knowing that variables defined in this block represent the central tendency
# of the distribution of random effects defined in the INDIVIDUAL_VARIABLES block (known as MU referencing).
# In this example the prefix "grp_" for a variable name could be a hint that this is a MU variable for NM-TRAN.
# Other model variables may be defined in this block according to user preference.


      GSIZE0=POP_SIZE0
   }# end GROUP_VARIABLES


   RANDOM_VARIABLE_DEFINITION{
# The RANDOM_VARIABLE_DEFINITION block is used to define the distribution of random variables.
      eta_PPV_SIZE0 ~ (type=Normal, mean=0, variance=PPV_SIZE0,level=ID)
      eta_PPV_TOVER ~ (type=Normal, mean=0, variance=PPV_TOVER,level=ID)
      eta_PPV_AE50 ~ (type=Normal, mean=0, variance=PPV_AE50,level=ID)
      eps_RUV_CV ~ (type=Normal, mean=0, variance=RUV_CV,level=DV)
      eps_RUV_SD ~ (type=Normal, mean=0, variance=RUV_SD,level=DV)
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
# The INDIVIDUAL_VARIABLES block is used to express the individual variables and their distribution.
# If this is not a population model then this block is not required.

      SIZE0=GSIZE0*exp(eta_PPV_SIZE0)
      GTOVER=POP_TOVER*168 # week -> h
      TOVER=GTOVER*exp(eta_PPV_TOVER)
      # gemcitabine AMT in data is umole (263g/mole)
      GAE50=POP_AE50/263*1000 # mg -> umole
      AE50=GAE50*exp(eta_PPV_AE50)
      # time in data is hours
      GTEQ=POP_TEQ*168 # week -> h
      TEQ=GTEQ
      KOVER=1/TOVER
      RIN=SIZE0*KOVER
      KPD=ln(2)/TEQ
      S1=1

   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{
# The MODEL_PREDICTION block calculates model predictions based on model variables defined in the previous model object blocks.
# Code in this block expresses the structural model after group and individual parameter differences have been included.
# It can be considered as an expression of a deterministic prediction. It is the essential component of a shared sub-model.
# This block should not contain any covariate computations expressing predictable or unpredictable (random) between subject differences.


      ## COMPARTMENTS
      ##
      ##    COMP = DRUG
      ##    COMP = TUMOUR

   ODE{
      # State variables for DE solver
      DAE=DRUG # amount in effect compartment
      DSIZE=TUMOUR # tumour size
      PD=1 - DAE/(AE50+DAE) # Emax pharmacodynamic model
      DRUG=ode(deriv= -KPD*DRUG ) # Bolus dose (in data) enters implicitly
      TUMOUR=ode(deriv= (RIN*PD - DSIZE*KOVER)*DSIZE,init=SIZE0)
   }# end ODE

   LIBRARY{
      ##  ADVAN13 TOL=9
      amount=nmadvan(model=13,param=list(S1,F,A,DRUG,TUMOUR))
   }# end LIBRARY

      # State variable solution
      AE=DRUG # amount in effect compartment
      SIZE=TUMOUR # tumour size
   #   Y=SIZE*(1 + ERR(1)) + ERR(2)
   #$TABLE ID TIME AMT CMT DV MDV DVID DOSE
   #AUCG AUCI AUCE TK0 SEX AGE RACE KPS STGE
   #NOAPPEND
   #ONEHEADER NOPRINT FILE=size.fit
   }# end MODEL_PREDICTION

   OBSERVATION{
      Y = SIZE+SIZE*eps_RUV_CV+eps_RUV_SD
   }# end OBSERVATION

} # end of model object
