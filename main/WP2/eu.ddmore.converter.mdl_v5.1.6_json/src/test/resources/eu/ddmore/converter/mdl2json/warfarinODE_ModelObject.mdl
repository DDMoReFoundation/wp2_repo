warfarin_PK_ODE_mdl = mdlobj{
   MODEL_INPUT_VARIABLES{ 
      ID: {use=id, level=2}
      TIME: {use=idv}
      logtWT: {use=covariate, type=continuous}  
      AMT: {use=amt}
      DV: {use=dv,level=1}
      MDV: {use=mdv}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      POP_CL
      POP_V
      POP_KA
      POP_TLAG
      BETA_WT_CL
      BETA_WT_V
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      PPV_CL
      PPV_V
      PPV_KA
      PPV_TLAG
      CORR_PPV_CL_V
      RUV_PROP
      RUV_ADD
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      ETA_CL ~ (type=normal, mean=0, sd=PPV_CL, level=ID)
      ETA_V ~ (type=normal, mean=0, sd=PPV_V, level=ID)
      ETA_KA ~ (type=normal, mean=0, sd=PPV_KA, level=ID)
      ETA_TLAG ~ (type=normal, mean=0, sd=PPV_TLAG, level=ID)

      # We define correlation here
      CORR_PPV_CL_V : {type=CORR, rv1=ETA_CL, rv2=ETA_V, level=ID}
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{

      # This maps to the "Type 3" individual parameter definition in PharmML
      CL: {type=linear, trans=log, pop=POP_CL, fixEff=[BETA_WT_CL], cov=[logtWT], ranEff = ETA_CL}
      V : {type=linear, trans=log, pop=POP_V, fixEff=[BETA_WT_V], cov=[logtWT], ranEff = ETA_V}
      KA : {type=linear, trans=log, pop=POP_KA, ranEff = ETA_KA}
      TLAG : {type=linear, trans=log, pop=POP_TLAG, ranEff = ETA_TLAG}
   }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      ODE{
          if (T>=TLAG){
            RATEIN = GUT*KA
          } else {
            RATEIN = 0
          } 
          GUT: {deriv =(-RATEIN), init=0, x0=0}
          CENTRAL: {deriv=(RATEIN - CL*CENTRAL/V),init=0,x0=0}
      } # end DEQ block
     CC = CENTRAL/V
   }# end MODEL_PREDICTION

   OBSERVATION{
      EPS_Y ~ (type=normal,mean=0,var=1, level=DV)

      # This maps the standard error model in PharmML. The error model is predefined. 
      Y : {type=continuous, error=combined1(additive=RUV_ADD, proportional=RUV_PROP, f=CC),eps=EPS_Y, prediction=CC}
   }# end OBSERVATION
 
   MODEL_OUTPUT_VARIABLES{
# The OUTPUT block specifies the model variables that may be exported from the model object.
      ID
      TIME
      logtWT
      CL
      V
      KA
      TLAG
      Y
   }# end MODEL_OUTPUT_VARIABLES

}