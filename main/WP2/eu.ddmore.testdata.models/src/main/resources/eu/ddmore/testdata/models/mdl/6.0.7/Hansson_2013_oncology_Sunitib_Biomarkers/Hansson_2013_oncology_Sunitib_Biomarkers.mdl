# Model based on the Hansson et al. CPT:PsP 2013
# PKPD modeling of VEGF, sVEGFR-2, sVEGFR-3 and sKIT following sunitinib treatment in GIST


Hansson2013_data = dataobj{
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      CYCL:{type=categorical}
      TIME:{type=continuous,units="h"}
      DAYS:{type=continuous,units="DAYS"}
      FLAG:{type=categorical} # 1=Dose ; 4=Tumor size (SLD); 5=VEGF ; 6=VEGFR2 ; 7=VEGFR2 ; 8=SKIT
      DVX:{type=continuous, units="mg/L"}
      DV:{type=continuous,units="mg/L"} # log transform data
      DOS:{type=continuous,units="mg"}
      PLA:{type=categorical} # placebo=1, treated=0
      CL:{type=continuous,units="L/h"}
      EVID:{type=categorical}
   }
   
   SOURCE{
       file="BIOMARKER_exDATA.csv"
       inputformat=nonmemFormat
       ignore="#"
   }
}

Hansson2013_par = parobj{

   STRUCTURAL{

      POP_IMAX:{value=1, fix=true} 
      POP_IC50:{value=1, lo=0, units="mg/L*h"} # Common IC50 for the four biomarkers which were found to be highly correlated

      #VEGF
      POP_BM0:{value=59.7, lo=0, units="pg/mL"} # baseline VEGF
      POP_MRT:{value=91, lo=0, units="h"} 
      POP_HILL:{value=3.31, lo=0}
      POP_SLP:{value=0.035, lo=-0.06, units="1/h"}
                
      #sVEGFR-2
      POP_BM02:{value=8670, lo=0, units="pg/mL"} # baseline sVEGFR-2
      POP_MRT2:{value=554, lo=0, units="h"} 
      POP_HILL2:{value=1.54, lo=0}
      
      #sVEGFR-3
      POP_BM03:{value=63900, lo=0, units="pg/mL"} # baseline sVEGFR-3
      POP_MRT3:{value=401, lo=0, units="h"} 

      #sKIT
      POP_BM0S:{value=39200, lo=0, units="pg/mL"} # baseline sVEGFR-3
      POP_MRTS:{value=2430, lo=0, units="h"}
      
      #Residual Error
      POP_RES_VEGF_ADD:{value=0.445, lo=0, units="pg/mL"} 
      POP_RES_sVEGFR2_ADD:{value=0.12, lo=0, units="pg/mL"} 
      POP_RES_sVEGFR2_PROP:{value=583, lo=0}
      POP_RES_sVEGFR3_ADD:{value=0.22, lo=0, units="pg/mL"}
      POP_RES_sKIT_ADD:{value=0.224, lo=0, units="pg/mL"}    
   }

   VARIABILITY{
      OMEGA_BM0:{value=0.252,type=VAR}
      OMEGA_BM2:{value=0.0369,type=VAR}
      OMEGA_BM3:{value=0.186,type=VAR}
      OMEGA_BMS:{value=0.254,type=VAR}
      
      OMEGA_MRT_VEGFs:{value=0.060, type=VAR}
      OMEGA_MRT_sKIT:{value=0.0753, type=VAR}

      OMEGA_IC50 : { value=0.253, type=VAR }
      OMEGA_IC502 : { value=0.189, type=VAR }
      OMEGA_IC503 : { value=0.398, type=VAR }
      OMEGA_IC50S : { value=5.77, type=VAR }

      CORR_IC50_IC502 : {type=COV, value=0.198 }
      CORR_IC50_IC503 : {type=COV, value=0.238 }
      CORR_IC50_IC50S : {type=COV, value=0.218 }
      CORR_IC502_IC503 : {type=COV, value=0.252 }
      CORR_IC502_IC50S : {type=COV, value=0.297 }
      CORR_IC503_IC50S : {type=COV, value=0.936 }

      #matrix(name="corrIC50s", type=VAR){
      #IC50,
      #0.198, IC502,
      #0.238, 0.252, IC503,
      #0.218, 0.297, 0.936, IC50S
      #}
      
      
      OMEGA_SLP:{value=2.95, type=VAR}
      OMEGA_SLPS:{value=3.01, type=VAR}     
          
      SIGMA_RES_W:{value=1 ,type=VAR, fix=true}
   }
}

### Model object
Hansson2013_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{type=categorical, use=id,level=2}
      CYCL:{type=categorical}
      TIME:{type=continuous, use=idv, units="h"}
      DAYS:{type=continuous,units="DAYS"}
      FLAG:{type=categorical, use=covariate} # 1=Dose ; 4=Tumor size (SLD); 5=VEGF ; 6=VEGFR2 ; 7=VEGFR3 ; 8=SKIT
      DVX:{type=continuous, units="mg/L"} 
      DV:{type=continuous,units="mg/L",use=dv, prediction=Y}
      DOS:{type=continuous,units="mg", use=amt}
      PLA:{type=categorical, use=covariate} # placebo=0, treated=1
      CL:{type=continuous,units="L/h", use=covariate}
      EVID:{type=categorical, use=evid}
   }# end MODEL_INPUT_VARIABLES

   STRUCTURAL_PARAMETERS{
      POP_IMAX
      POP_IC50 # Common IC50 for the four biomarkers which were found to be highly correlated

      #VEGF
      POP_BM0  # baseline VEGF
      POP_MRT 
      POP_HILL
      POP_TVSLP
                
      #sVEGFR-2
      POP_BM02
      POP_MRT2 
      POP_HILL2
      
      #sVEGFR-3
      POP_BM03
      POP_MRT3 

      #sKIT
      POP_BM0S
      POP_MRTS
      
      #Residual Error
      POP_RES_VEGF_ADD 
      POP_RES_sVEGFR2_ADD 
      POP_RES_sVEGFR2_PROP
      POP_RES_sVEGFR3_ADD
      POP_RES_sKIT_ADD        
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_BM0
      OMEGA_BM02
      OMEGA_BM03
      OMEGA_BM0S
      
      OMEGA_MRT_VEGFs
      OMEGA_MRT_sKIT
      
      OMEGA_IC50
      OMEGA_IC502
      OMEGA_IC503
      OMEGA_IC50S
              
      OMEGA_TVSLP
      OMEGA_TVSLPS     
      SIGMA_RES_W
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      eta_BM0 ~ {type=normal, mean=0, var=OMEGA_BM0, level=ID}
      eta_BM02 ~ {type=normal, mean=0, var=OMEGA_BM02, level=ID}
      eta_BM03 ~ {type=normal, mean=0, var=OMEGA_BM03, level=ID}
      eta_BM0S ~ {type=normal, mean=0, var=OMEGA_BM0S, level=ID}
      
      eta_MRT_VEGFs ~ {type=normal, mean=0, var=OMEGA_MRT_VEGFs, level=ID}
      eta_MRT_sKIT ~ {type=normal, mean=0, var=OMEGA_MRT_sKIT, level=ID}
   
      eta_IC50 ~ {type=normal, mean=0, var=OMEGA_IC50, level=ID}
      eta_IC502 ~ {type=normal, mean=0, var=OMEGA_IC502, level=ID}
      eta_IC503 ~ {type=normal, mean=0, var=OMEGA_IC503, level=ID}
      eta_IC50S ~ {type=normal, mean=0, var=OMEGA_IC50S, level=ID}
      
      eta_TVSLP ~ { type=normal, mean=0, var=OMEGA_TVSLP, level=ID}
      eta_TVSLPS ~ { type=normal, mean=0, var=OMEGA_TVSLPS, level=ID}   

      CORR_IC50_IC502 : {type=COV, rv1=eta_IC50, rv2=eta_IC502, level=ID }
      CORR_IC50_IC503 : {type=COV, rv1=eta_IC50, rv2=eta_IC503, level=ID }
      CORR_IC50_IC50S : {type=COV, rv1=eta_IC50, rv2=eta_IC50S, level=ID }
      CORR_IC502_IC503 : {type=COV, rv1=eta_IC502, rv2=eta_IC503, level=ID }
      CORR_IC502_IC50S : {type=COV, rv1=eta_IC502, rv2=eta_IC50S, level=ID }
      CORR_IC503_IC50S : {type=COV, rv1=eta_IC503, rv2=eta_IC50S, level=ID }
}# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      BM0 : {type=linear, trans=log, pop = POP_BM0, ranEff=eta_BM0} 
      BM02 : {type=linear, trans=log, pop = POP_BM02, ranEff=eta_BM02} 
      BM03 : {type=linear, trans=log, pop = POP_BM03, ranEff=eta_BM03}
      BM0S : {type=linear, trans=log, pop = POP_BM0S, ranEff=eta_BM0S} 
               
      IMAX1 = POP_IMAX
      IMAX2 = POP_IMAX
      IMAX3 = POP_IMAX
      IMAXS = POP_IMAX
     
      IC50 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC50} 
      IC502 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC502} 
      IC503 : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC503}
      IC50S : {type=linear, trans=log, pop = POP_IC50, ranEff=eta_IC50S} 
 
      HILL = POP_HILL
      HILL2 = POP_HILL2
     
      MRT1 : {type=linear, trans=log, pop = POP_MRT, ranEff=eta_MRT_VEGFs} 
      MRT2 : {type=linear, trans=log, pop = POP_MRT2, ranEff=eta_MRT_VEGFs} 
      MRT3 : {type=linear, trans=log, pop = POP_MRT3, ranEff=eta_MRT_VEGFs}
      MRTS : {type=linear, trans=log, pop = POP_MRTS, ranEff=eta_MRT_sKIT} 
     
      TVSLP = POP_TVSLP/1000
      DPSLP : {type=linear, trans=log, pop = TVSLP, ranEff=eta_TVSLP} 
         
      TVSLPS = POP_TVSLP/1000
      DPSLPS : {type=linear, trans=log, pop = TVSLPS, ranEff=eta_TVSLPS} 
         
      # Derived parameters
      KOUT = 1/MRT1
      KOUT2 = 1/MRT2
      KOUT3 = 1/MRT3
      KOUTS = 1/MRTS

      KIN2 = BM02*KOUT2
      KIN3 = BM03*KOUT3
     
      }# end INDIVIDUAL_VARIABLES


   MODEL_PREDICTION{
      AUC = DOS/CL
      DP1 = BM0*(1+DPSLP*TIME)        # Linear disease progression model for VEGF
      DPS = BM0S*(1+DPSLPS*TIME)      # Linear disease progression model for SKIT

      KIN = DP1*KOUT
      KINS = DPS*KOUTS
       
       ODE{
       EFF = IMAX1*AUC^HILL /(IC50^HILL+AUC^HILL)     # VEGF  : sigmoid Imax drug effect relationship
       EFF2 = IMAX2*AUC^HILL2/(IC502^HILL2+AUC^HILL2) # VEGFR2: sigmoid Imax drug effect relationship
       EFF3 = IMAX3*AUC/(IC503+AUC)                      # V EGFR3: Imax drug effect relationship
       EFFS = IMAXS*AUC/(IC50S+AUC)                      # SKIT  : Imax drug effect relationship
       
       VEGF : {deriv=KIN -KOUT*(1-EFF)*VEGF, init=BM0, wrt=T}
       sVEGFR2 : {deriv=KIN2*(1-EFF2)-KOUT2*sVEGFR2, init=BM02, wrt=T}
       sVEGFR3 : {deriv=KIN3*(1-EFF3)-KOUT3*sVEGFR3, init=BM03, wrt=T}
       sKIT : {deriv= KINS*(1-EFFS)-KOUTS*sKIT, init=BM0S, wrt=T}
    }
   }# end MODEL_PREDICTION

 
   OBSERVATION{
      eps_RES_W ~ {type=normal, mean=0, var=SIGMA_RES_W, level=DV}
                      
   	  VEGF_obs : {type=continuous, trans=log, error=additiveError(additive=POP_RES_VEGF_ADD), prediction=VEGF, eps=eps_RES_W}
      
      sVEGFR2_obs : {type=continuous, trans=log, error=combinedError3(additive=POP_RES_sVEGFR2_ADD, proportional=POP_RES_sVEGFR2_PROP,f=sVEGFR2),
       prediction=sVEGFR2, eps=eps_RES_W}
       # Maybe a 3rd combined error is needed if we are in the log scale W = sqrt(POP_RES_sVEGFR2_ADD^2+(POP_RES_sVEGFR2_PROP/sVEGFR2)^2) 
  	  
  	  sVEGFR3_obs : {type=continuous, trans=log, error=additiveError(additive=POP_RES_sVEGFR3_ADD), prediction=sVEGFR3, eps=eps_RES_W}
  	  
  	  sKIT_obs : {type=continuous, trans=log, error=additiveError(additive=POP_RES_sKIT_ADD), prediction=sKIT, eps=eps_RES_W}

	  Y = VEGF_obs when DVID == 4, 
	  	sVEGFR2_obs when DVID == 5,
	    sVEGFR3_obs when DVID == 6,
	    sKIT_obs when DVID == 7;
	   #Comparison operator is ==
	   #You have to use ; to finish the piecewise function without otherwise statement  
       #DVID is undefined
   }# end OBSERVATION

} # end of model object
       
       
Hansson2013_task = taskobj{
### Task Properties object

    ESTIMATE { 
    	target=NMTRAN_CODE 
        cov=true
        algo= ["FOCE"] 
	}
}# end of task object

Hansson2013 = mog{
	Hansson2013_data
	Hansson2013_par
	Hansson2013_mdl
	Hansson2013_task
}